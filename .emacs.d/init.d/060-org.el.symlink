;; -*- mode: emacs-lisp -*-

(use-package org
  :mode ("\\.org\\'" . org-mode)

  :bind (("C-c a" . org-agenda)
         ("C-c c" . org-capture)
         ("C-c l" . org-store-link)
         :map org-mode-map
         ("C-c C-a" . michalrus/org-archive-done-tasks-in-file)
         ("C-c C-v" . org-tasklist-edit-value))

  :init
  (setq org-directory "~/Org")
  (setq-default
     org-reverse-note-order t
     org-refile-targets '((nil . (:maxlevel . 4)))
     org-refile-use-outline-path t
     org-todo-keywords '((sequence "TODO(t!)" "IN-PROGRESS(i!)" "WAITING(w@)" "|" "DONE(d!)" "DELEGATED(g@)" "CANCELED(c@)"))
     org-log-into-drawer t
     org-startup-indented t
     org-log-done nil
     org-support-shift-select t
     org-catch-invisible-edits 'error
     org-ctrl-k-protect-subtree 'error)

  (use-package org-id
    :init
    (setq org-id-link-to-org-use-id t))

  :config
  (setq org-default-notes-file (expand-file-name "Inbox.org" org-directory))
  ;; Unbind S-left/right, because I’m logging every state change!
  (dolist (dir '("left" "right"))
    (define-key org-mode-map (kbd (format "S-<%s>" dir)) nil))
  (setq org-clock-persist 'history)
  (org-clock-persistence-insinuate)

  (defun org-tasklist-is-1st-level-task-p (pom)
    "Will return `t' iff the task at `pom' is a TODO/DONE/… at the first level, i.e. not a subtask of another TODO/DONE/…, `nil' otherwise."
    (interactive (list (point)))
    (save-excursion
      (goto-char pom)
      (and (or (org-entry-is-todo-p)
               (org-entry-is-done-p))
           (or (eq (org-current-level) 1)
               (let* ((check-parent (lambda ()
                                      (save-excursion
                                        (let ((point-was-at (point)))
                                          (condition-case nil
                                              (outline-up-heading 1 t)
                                            (error nil))
                                          (cond ((or (org-entry-is-done-p) (org-entry-is-todo-p)) nil)
                                                ((eq (point) point-was-at) t)
                                                (t (funcall check-parent))))))))
                 (funcall check-parent))))))

  ;; ——————————————————— org-tasklist sketch (BEGIN) ———————————————————

  (setq org-agenda-custom-commands
        '(("t" "Tasklist" alltodo ""
           ((org-agenda-files '("~/Org/Tasklist.org"))
            (org-agenda-todo-list-sublevels nil)
            (org-agenda-prefix-format
             (let ((cpy (copy-alist org-agenda-prefix-format)))
               (progn (setcdr (assq 'todo cpy)
                              "%(org-tasklist--agenda-prefix-format)  %-11:c  ")
                      cpy)))
            ;;(org-agenda-overriding-header "Projects waiting for something: ")
            ))))

  (defun org-tasklist--agenda-prefix-format ()
    (let ((value      (org-entry-get (point) "TASKLIST_VALUE"))
           (investment (org-entry-get (point) "TASKLIST_INVESTMENT")))
      (cond ((and value investment)
             (let ((nvalue (string-to-number value))
                   (ninvestment (string-to-number investment))
                   ;; TODO: read units & labels from :PROPERTIES:
                   (unit-value "$")
                   (unit-investment "hr"))
               (format "%6.02f %s/%s"
                       (/ nvalue ninvestment)
                       (substring unit-value 0 1)
                       (substring unit-investment 0 1))))
            (t (format "%6s %3s" "——" "")))))

  (defun org-tasklist-edit-value (pom)
    (interactive (list (point)))
    (unless (org-tasklist-is-1st-level-task-p pom)
      (error (format "Entry at %d is not a 1st-level task, aborting" pom)))
    (let* ((read-num (lambda (label unit default)
                       (let* ((str (read-string (format "%s [%s] (%f): " label unit default)
                                                nil nil (number-to-string default)))
                              (num (string-to-number str)))
                         (if (eq num 0) (error (format "%s [%s] must be a non-zero number, got ‘%s’" label unit str))) num)))
           ;; TODO: read units & labels from :PROPERTIES:
           (value      (funcall read-num "value" "$" 25.0))
           (investment (funcall read-num "time"  "hr" 0.5)))
      (org-entry-put pom "TASKLIST_VALUE" (number-to-string value))
      (org-entry-put pom "TASKLIST_INVESTMENT" (number-to-string investment))))

  ;; ——————————————————— org-tasklist sketch (END) ———————————————————

  (defun michalrus/org-archive-done-tasks-in-file ()
    "Archives all done tasks in the current Org file to their “Archive” siblings"
    (interactive)
    (org-map-entries
     (lambda ()
       (let ((entry-tags (upcase (or (org-entry-get (point) "ALLTAGS") ""))))
         (cond ((or (string-match-p ":ARCHIVE:"  entry-tags)
                    (string-match-p ":SKIP_ARCHIVE:" entry-tags))
                ;; Sometimes we’re left with (point) at an :ARCHIVE: after
                ;; (org-archive-to-archive-sibling) returns and this slips
                ;; through the 'archive filter of (org-map-entries), and
                ;; results in an infinite recursion, so… yeah. Let’s
                ;; recheck manually and skip the archived subtree.
                (setq org-map-continue-from (+ 1 (save-excursion (org-end-of-subtree t)))))
               ((org-entry-is-done-p)
                (let ((point-was-at (point)))
                  (org-archive-to-archive-sibling)
                  ;; Continue mapping at where the archived entry was,
                  ;; because the next one (its ex-sibling) might need
                  ;; archiving, too.
                  (setq org-map-continue-from point-was-at))))))
     t 'file 'archive)))
